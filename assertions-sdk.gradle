apply plugin: 'maven-publish'
apply plugin: 'signing'

/**
 * This script is responsible for assertions-sdk publishing to maven repository.
 */
ext {

    /**
     * This is list of libraries that we are going to publish.
     * Also it declares mapping from local module names to public names.
     */
    assertionsSdkLibNames = [
            libJavaAssertions   : 'assertions-java',
            libKotlinAssertions : 'assertions-kotlin',
            libAndroidAssertions: 'assertions-android'
    ]
}

publishing {
    publications {
        maven(MavenPublication) {

            groupId assertionSdkGroup
            artifactId assertionsSdkLibNames[project.name]
            version assertionSdkVersion

            pom {
                name = 'Assertions library'
                description = 'Assertions library for Android applications'
                url = 'https://github.com/heershingenmosiken/assertions-android'

                developers {
                    developer {
                        name = 'Stanislav Dekalo'
                        email = 'dekalo.stanislav@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/heershingenmosiken/assertions-android/'
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
    repositories {
        /**
         * Install to local repository
         */
        maven {
            url System.getProperty("user.home") + "/.m2/repository"
        }
    }
}

signing {
    def loadedSigningKey = getSecretValue("signingKey")
    def loadedSigningPassword = getSecretValue("signingPassword")
    useInMemoryPgpKeys(loadedSigningKey, loadedSigningPassword)
    sign publishing.publications.maven
}

