apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply from: '../passwords.gradle'

/**
 * This script is responsible for assertions-sdk publishing to maven repository.
 */
ext {
    assertionsSdkGroupId = 'com.heershingenmosiken'
    assertionsSdkVersion = '1.1.1-SNAPSHOT'

    /**
     * This is list of libraries that we are going to publish.
     * Also it declares mapping from local module names to public names.
     */
    assertionsSdkLibNames = [
            libAssertions       : 'assertions-java',
            libAndroidAssertions: 'assertions-android'
    ]
}

bintray {
    user = getAssertionsBintrayUser()
    key = getAssertionsBintrayApiKey()
    publications = ['maven']
    publish = true
    pkg {
        repo = 'heershingenmosiken'
        name = assertionsSdkLibNames[project.name]
        userOrg = 'dekalo-stanislav'
        vcsUrl = 'https://github.com/heershingenmosiken/android-assertions'

        version {
            name = assertionsSdkVersion
            released = new Date()
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {

            groupId assertionsSdkGroupId
            artifactId assertionsSdkLibNames[project.name]
            version assertionsSdkVersion

            pom {
                name = 'Assertions library'
                description = 'Android client for gfycat.com'
                url = 'https://github.com/heershingenmosiken/android-assertions'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    // api already included in configurations.implementation dependencies list.
                    configurations.implementation.allDependencies.each {

                        if (it.group != null && (it.name != null && "unspecified" != it.name) && (it.version != null && "unspecified" != it.version)) {
                            /**
                             * Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                             */
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        } else if (assertionsSdkLibNames[it.name]) {
                            /**
                             * It is local project dependency and it is declared in sdkLibNames above.
                             */
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', assertionsSdkGroupId)
                            dependencyNode.appendNode('artifactId', assertionsSdkLibNames[it.name])
                            dependencyNode.appendNode('version', assertionsSdkVersion)
                        }
                    }
                }
            }
        }
    }
    repositories {
        /**
         * Install to local repository
         */
        maven {
            url System.getProperty("user.home") + "/.m2/repository"
        }
    }
}
